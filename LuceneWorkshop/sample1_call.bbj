SETESC HANDLE_ESCAPE
SETERR HANDLE_ERROR
REM
REM "Sample Character Data Entry Program
REM
rem PRINT 'MAXIMIZE','FL',"2"+$0601FF$

PRINT 'FL',"2"+$0601FF$
TERMS$=STBL("!TERMS",STBL("!TERMS")+$FF07$)
REM ***** File Initialization *****
LET CUSTOMER=UNT
OPEN (CUSTOMER)"CUST1.DAT"
DIM CUSTOMER$:"cust_num:n(4),last_name:c(20*),first_name:c(20*),sales_region:c(8*),address:c(40*),address2:c(40*),city:c(15*),st:c(2*),zip:c(8*),phone:c(15*),max_credit:n(10*),curr_bal:n(12*),terms:c(10*)"
LET BLANK_CUSTOMER$=CUSTOMER$
LET COMPANY$=STBL("COMPANY")
LET TITLE$="Customer File Maintenance"
LET TODAY$=DATE(0)
LET PROGRAM$="sample1.bbj"
REM ***** Function And Constant Definitions *****
LET EQUAL$=FILL(80,"=")
LET MASK1$="###,###,##0.00"
LET MASK2$="(##,###,##0.00)"
REM ***** Screen Initialization *****
CALL "header.bbj",COMPANY$,TITLE$,TODAY$,PROGRAM$,EQUAL$
PRINT 'SB',@(5,4)," 1. Customer Number:",@(5,5)," 2. Last Name:",@(5,6)," 3. First Name:",@(5,7)," 4. Sales Region:",@(5,8)," 5. Address 1:",@(5,9)," 6. Address 2:",@(5,10)," 7. City:",@(5,11)," 8. State:",@(5,12)," 9. Zip Code:",@(5,13),"10. Telephone:",@(5,14),"11. Max Credit:",@(5,15),"12. Current Balance:",@(5,16),"13. Terms:",'SF',
REM ***** Detail Inputs *****
CUSTOMER_NUMBER:
LET PROMPT$="F2-Next, F3-Prev, F4-Exit"
LET ROW=4,COL=30,LENGTH=40,VALUE$="",TYPE$="C"
LET DOING_CUST_NUM=1,CUSTOMER$=BLANK_CUSTOMER$,NEW_RECORD=1
PRINT 'CF',
GOSUB DO_INPUT
ON CTL_VAL GOTO DO_CUSTOMER,CUSTOMER_NUMBER,GET_NEXT,GET_PREVIOUS,EXIT_PROGRAM
DO_CUSTOMER:
IF VALUE$="" THEN GOTO CUSTOMER_NUMBER
LET CUSTOMER.CUST_NUM=NUM(VALUE$,err=DO_FULLTEXT)
GOTO DONE_FULLTEXT

DO_FULLTEXT:

call "FulltextSearch_Call.bbj",VALUE$
IF VALUE$="" THEN
    GOTO DO_CUSTOMER
ELSE
    CUSTOMER.CUST_NUM=NUM(VALUE$,err=DO_CUSTOMER)
FI

DONE_FULLTEXT:



EXTRACT RECORD(CUSTOMER,KEY=CUSTOMER.CUST_NUM$,DOM=ADD_RECORD)CUSTOMER$
LET NEW_RECORD=0
GOSUB DISPLAY_RECORD
GOTO LAST_NAME
ADD_RECORD:
INPUT (0,ERR=ADD_RECORD)@(0,23),'CE',@(20),"Add this record? (N/Y):  ",ANSWER$:(""=CUSTOMER_NUMBER,"Y"=LAST_NAME,"y"=LAST_NAME,"N"=CUSTOMER_NUMBER,"n"=CUSTOMER_NUMBER)
LAST_NAME:
LET PROMPT$="F2-Next, F3-Prev, F7-Delete, F4-Back"
LET ROW=5,COL=30,LENGTH=20,VALUE$=CUSTOMER.LAST_NAME$,TYPE$="C"
LET DOING_CUST_NUM=0
GOSUB DO_INPUT
ON CTL_VAL GOTO DO_LAST_NAME,LAST_NAME,GET_NEXT,GET_PREVIOUS,CUSTOMER_NUMBER,LAST_NAME,LAST_NAME,DO_DELETE
DO_LAST_NAME:
IF VALUE$=$$ THEN IF CUSTOMER.LAST_NAME$<>"" THEN GOTO FIRST_NAME ELSE GOTO LAST_NAME
LET CUSTOMER.LAST_NAME$=VALUE$
FIRST_NAME:
LET PROMPT$="F4-Back",ROW=6,COL=30,LENGTH=15,VALUE$=CUSTOMER.FIRST_NAME$,TYPE$="C"
GOSUB DO_INPUT
IF CTL_VAL=4 THEN GOTO LAST_NAME
IF CTL_VAL THEN GOTO FIRST_NAME
IF VALUE$=$$ THEN IF CUSTOMER.FIRST_NAME$<>"" THEN GOTO SALES_REGION ELSE GOTO FIRST_NAME
LET CUSTOMER.FIRST_NAME$=VALUE$
SALES_REGION:
LET PROMPT$="F4-Back",ROW=7,COL=30,LENGTH=8,VALUE$=CUSTOMER.SALES_REGION$,TYPE$="C"
GOSUB DO_INPUT
IF CTL_VAL=4 THEN GOTO FIRST_NAME
IF CTL_VAL THEN GOTO SALES_REGION
LET CUSTOMER.SALES_REGION$=VALUE$
ADDRESS_1:
LET PROMPT$="F4-Back",ROW=8,COL=30,LENGTH=40,VALUE$=CUSTOMER.ADDRESS$,TYPE$="C"
GOSUB DO_INPUT
IF CTL_VAL=4 THEN GOTO SALES_REGION
IF CTL_VAL THEN GOTO ADDRESS_1
LET CUSTOMER.ADDRESS$=VALUE$
IF VALUE$ = "" THEN GOTO TELEPHONE
ADDRESS_2:
LET PROMPT$="F4-Back",ROW=9,COL=30,LENGTH=40,VALUE$=CUSTOMER.ADDRESS2$,TYPE$="C"
GOSUB DO_INPUT
IF CTL_VAL=4 THEN GOTO ADDRESS_1
IF CTL_VAL THEN GOTO ADDRESS_2
LET CUSTOMER.ADDRESS2$=VALUE$
CITY:
LET PROMPT$="F4-Back",ROW=10,COL=30,LENGTH=15,VALUE$=CUSTOMER.CITY$,TYPE$="C"
GOSUB DO_INPUT
IF CTL_VAL=4 THEN GOTO ADDRESS_2
IF CTL_VAL THEN GOTO CITY
LET CUSTOMER.CITY$=VALUE$
STATE:
LET PROMPT$="F4-Back",ROW=11,COL=30,LENGTH=2,VALUE$=CUSTOMER.ST$,TYPE$="C"
GOSUB DO_INPUT
IF CTL_VAL=4 THEN GOTO CITY
IF CTL_VAL THEN GOTO STATE
LET CUSTOMER.ST$=VALUE$
ZIP_CODE:
LET PROMPT$="F4-Back",ROW=12,COL=30,LENGTH=8,VALUE$=CUSTOMER.ZIP$,TYPE$="C"
GOSUB DO_INPUT
IF CTL_VAL=4 THEN GOTO STATE
IF CTL_VAL THEN GOTO ZIP_CODE
LET CUSTOMER.ZIP$=VALUE$
TELEPHONE:
LET PROMPT$="F4-Back",ROW=13,COL=30,LENGTH=15,VALUE$=CUSTOMER.PHONE$,TYPE$="C"
GOSUB DO_INPUT
IF CTL_VAL=4 THEN GOTO ZIP_CODE
IF CTL_VAL THEN GOTO TELEPHONE
LET CUSTOMER.PHONE$=VALUE$
MAX_CREDIT:
LET PROMPT$="F4-Back",ROW=14,COL=30,LENGTH=10,VALUE$=CUSTOMER.MAX_CREDIT$,TYPE$="N"
GOSUB DO_INPUT
IF CTL_VAL=4 THEN GOTO TELEPHONE
IF CTL_VAL THEN GOTO MAX_CREDIT
LET CUSTOMER.MAX_CREDIT=NUM(VALUE$)
CURRENT_BALANCE:
LET PROMPT$="F4-Back",ROW=15,COL=30,LENGTH=12,VALUE$=CUSTOMER.CURR_BAL$,TYPE$="N"
CURRENT_BALANCE2:
GOSUB DO_INPUT
IF CTL_VAL=4 THEN GOTO MAX_CREDIT
IF CTL_VAL THEN GOTO CURRENT_BALANCE
IF NUM(VALUE$) <= CUSTOMER.MAX_CREDIT THEN GOTO GOOD_BAL
LET PROMPT$="The Current Balance may not exceed the Max Credit"
print(0)'rb'
goto CURRENT_BALANCE2
GOOD_BAL:
LET CUSTOMER.CURR_BAL=NUM(VALUE$)
TERMS:
LET PROMPT$="F4-Back",ROW=16,COL=30,LENGTH=10,VALUE$=CUSTOMER.TERMS$,TYPE$="C"
GOSUB DO_INPUT
IF CTL_VAL=4 THEN GOTO CURRENT_BALANCE
IF CTL_VAL THEN GOTO TERMS
LET CUSTOMER.TERMS$=VALUE$
SAVE_RECORD:
GOSUB DISPLAY_RECORD
INPUT (0,ERR=SAVE_RECORD)@(10,23),'CE',"Save this record? (N/Y):  ",ANSWER$:("Y"=DO_SAVE,"y"=DO_SAVE,"N"=DO_NOT_SAVE,"n"=DO_NOT_SAVE,""=DO_NOT_SAVE)
DO_NOT_SAVE:
INPUT (0,ERR=DO_NOT_SAVE)@(10,23),'CE',"Abandon changes or change more? (A/C):  ",ANSWER$:("A"=ABANDON,"a"=ABANDON,"C"=CHANGE,"c"=CHANGE,""=ABANDON)
DO_DELETE:
IF NEW_RECORD THEN GOTO CUSTOMER_NUMBER
INPUT (0,ERR=DO_DELETE)@(10,23),"Delete this record? (N/Y):  ",ANSWER$:(""=LAST_NAME,"N"=LAST_NAME,"n"=LAST_NAME,"Y"=DELETE_IT,"y"=DELETE_IT)
DO_SAVE:
LET CUSTOMER$=FIELD(CUSTOMER$)
WRITE RECORD(CUSTOMER)CUSTOMER$
GOTO ABANDON
CHANGE:
PRINT @(0,23),'CE',
GOTO TERMS
DELETE_IT:
REMOVE (CUSTOMER,KEY=CUSTOMER.CUST_NUM$)
ABANDON:
PRINT 'CF'
GOTO CUSTOMER_NUMBER
GET_NEXT:
READ (CUSTOMER,END=NO_MORE_RECORDS)
LET MY_KEY$=KEY(CUSTOMER,END=NO_MORE_RECORDS)
EXTRACT RECORD(CUSTOMER,KEY=MY_KEY$)CUSTOMER$
GOSUB DISPLAY_RECORD
LET NEW_RECORD=0
GOTO LAST_NAME
NO_MORE_RECORDS:
REM INPUT (0,ERR=NO_MORE_RECORDS)@(10,23),'CE',"No more records!  ",*
INPUT (0,ERR=NO_MORE_RECORDS)@(10,23),'CE',"No more records!  ",AAAA$
PRINT @(10,23),'CE',
IF DOING_CUST_NUM THEN GOTO CUSTOMER_NUMBER ELSE GOTO LAST_NAME
GET_PREVIOUS:
LET MY_KEY$=KEYP(CUSTOMER,END=NO_MORE_RECORDS)
EXTRACT RECORD(CUSTOMER,KEY=MY_KEY$)CUSTOMER$
GOSUB DISPLAY_RECORD
LET NEW_RECORD=0
GOTO LAST_NAME
EXIT_PROGRAM:
IF CUSTOMER THEN CLOSE (CUSTOMER)
IF TCB(13) THEN EXIT ELSE RELEASE
REM ***** Standard Subroutines *****
DO_INPUT:
CALL "myinput.bbj",PROMPT$,ROW,COL,LENGTH,VALUE$,TYPE$,CTL_VAL
RETURN
DISPLAY_RECORD:
PRINT @(30,4),'CL',CUSTOMER.CUST_NUM,
PRINT @(30,5),'CL',CUSTOMER.LAST_NAME$,
PRINT @(30,6),'CL',CUSTOMER.FIRST_NAME$,
PRINT @(30,7),'CL',CUSTOMER.SALES_REGION$,
PRINT @(30,8),'CL',CUSTOMER.ADDRESS$,
PRINT @(30,9),'CL',CUSTOMER.ADDRESS2$,
PRINT @(30,10),'CL',CUSTOMER.CITY$,
PRINT @(30,11),'CL',CUSTOMER.ST$,
PRINT @(30,12),'CL',CUSTOMER.ZIP$,
PRINT @(30,13),'CL',CUSTOMER.PHONE$,
PRINT @(30,14),'CL',CUSTOMER.MAX_CREDIT:MASK1$,
PRINT @(30,15),'CL',CUSTOMER.CURR_BAL:MASK2$,
PRINT @(30,16),'CL',CUSTOMER.TERMS$,
RETURN
REM ***** Exception Handling Logic *****
HANDLE_ESCAPE:
PRINT "Escape detected"
RELEASE
HANDLE_ERROR:
PRINT 'WINDOW'(20,8,40,8,"Error Occurred In Program!"),'CS',
PRINT "An error "+STR(ERR)+" occurred at line "+STR(TCB(5))+"."
REM INPUT *
INPUT AAA$
PRINT 'POP'
GOTO EXIT_PROGRAM
END
